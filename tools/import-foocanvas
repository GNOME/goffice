#!/usr/bin/perl -w
# -----------------------------------------------------------------------------

use strict;

$| = 1;

my $myself = $0;
$myself =~ s|^.*/||;

die "$0: must run from top-level goffice directory.\n"
    unless -r "goffice/goffice.h";
my $dst = "goffice/cut-n-paste/foocanvas";

my $foodir = $ARGV[0];
$foodir = "../foocanvas" unless defined $foodir;
die "$0: must specify foocanvas directory on command line.\n"
    unless -d $foodir;

-d $dst or mkdir $dst or
    die "$0: cannot mkdir $dst: $!\n";

&copy_subsystem ("libfoocanvas");

# -----------------------------------------------------------------------------

sub copy_subsystem {
    my ($sys) = @_;

    my $srcdir = "$foodir/$sys";
    my $dstdir = "$dst";

    -d $dstdir or mkdir "$dstdir" or
	die "$0: cannot mkdir $dstdir: $!\n";

    my @sources;

    my $have_cvsignore = (-r "$srcdir/.cvsignore");
    push @sources, ".cvsignore" if $have_cvsignore;

    local (*SRC,*DST);
    open (SRC, "<$srcdir/Makefile.am") or
	die "$0: Cannot read $srcdir/Makefile.am: $!\n";
    open (DST, ">$dstdir/Makefile.am.new") or
	die "$0: Cannot write $dstdir/Makefile.am.new: $!\n";
    print STDERR "Creating $dstdir/Makefile.am...";
    while (<SRC>) {
	next if /^pkgconfig/ ... !/\\\s*$/;

	s|/libfoocanvas-1.0/libfoocanvas|/libgoffice-1/goffice/cut-n-paste/foocanvas/|;
	s|\$\(LIBFOOCANVAS_CFLAGS\)|\$\(GOFFICE_CFLAGS\)|;
	s/lib_LTLIBRARIES/noinst_LTLIBRARIES/;

	if (/^[a-zA-Z0-9_]+_(HEADERS|la_SOURCES)\s*=/ ... !/\\\s*$/) {
	    my $line = $_;
	    $line =~ s/^.*=|^\s+|\s*\\|\s+$|\$\(libfoocanvasinclude_HEADERS\)//g;
	    push @sources, split (' ', $line) if $line ne '';
	}

	next if /^(libfoocanvas_a_LIBADD|EXTRA_DIST)\s*=/ ... !/\\\s*$/;
	next if /^\s*\\$/;

	print DST;
    }

    close (*SRC);
    close (*DST);
    &update_file ("$dstdir/Makefile.am");

    if (!$have_cvsignore) {
	open (DST, ">$dstdir/.cvsignore.new") or
	    die "$0: Cannot write $dstdir/.cvsignore.new: $!\n";
	print STDERR "Creating $dstdir/.cvsignore...";
	print DST "Makefile.in\n";
	print DST "Makefile\n";
	print DST ".deps\n";
	print DST ".libs\n";
	close (*DST);
	&update_file ("$dstdir/.cvsignore");
    }

    foreach my $file (@sources) {
	local (*SRC,*DST);
	open (SRC, "<$srcdir/$file") or
	    die "$0: Cannot read $srcdir/$file: $!\n";
	open (DST, ">$dstdir/$file.new") or
	    die "$0: Cannot write $dstdir/$file.new: $!\n";

	print STDERR "Creating $dstdir/$file...";

	print DST "/* File import from foocanvas to libgoffice by $myself.  Do not edit.  */\n\n";

	if ($file =~ /\.c$/) {
	    print DST "#undef GTK_DISABLE_DEPRECATED\n"
		unless $file =~ /^(foo-canvas-rect-ellipse|foo-canvas-util|foo-canvas)\.c$/;

	    print DST "#include <goffice-config.h>\n";
	    print DST "#include <glib/gi18n.h>\n";
	}

      LINE:
	while (<SRC>) {
	    chomp;
	    if (m{^\s*\#\s*include\s*[<\"]libfoocanvas/(.*)[>\"]}) {
		print DST "#include \<goffice/cut-n-paste/foocanvas/$1\>\n";
		next LINE;
	    }

	    s/\s+$//;

	    next if (/^\s*\* \$[I][d]: .* [E][x][p] \$\s*$/);
	    next if (/^\s*\#\s*include\s*[<\"]config\.h[>\"]/);

	    # Turn C++ comments into C comments.
	    if (m|^\s*//|) {
		s|//|/*|;
		s|$|*/|;
	    }

	    print DST "$_\n";
	}
	close (*SRC);
	close (*DST);

	&update_file ("$dstdir/$file");
    }
}

# -----------------------------------------------------------------------------

sub update_file {
    my ($old) = @_;
    my ($new) = "$old.new";

    if (!-r $old) {
	rename $new, $old or
	    die "$0: Cannot rename $new to $old: $!\n";
	print STDERR " -- done.\n";
    } else {
	system ("cmp '$old' '$new' >/dev/null");
	if ($? == 0) {
	    print STDERR " -- unchanged.\n";
	    unlink $new;
	} else {
	    rename $new, $old or
		die "$0: Cannot rename $new to $old: $!\n";
	    print STDERR " -- done.\n";
	}
    }
}

# -----------------------------------------------------------------------------
