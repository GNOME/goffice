#!/usr/bin/perl -w
# -----------------------------------------------------------------------------

use strict;

$| = 1;

my $myself = $0;
$myself =~ s|^.*/||;

die "$0: must run from top-level goffice directory.\n"
    unless -r "goffice/goffice.h";
my $dst = "goffice/cut-n-paste/pcre";

my $srcdir = $ARGV[0];
$srcdir = "../pcre" unless defined $srcdir;
die "$0: must specify pcre directory on command line.\n"
    unless -d $srcdir;

-d $dst or mkdir $dst or
    die "$0: cannot mkdir $dst: $!\n";

my $force_utf8 = 1;

my $configfile = "<goffice/goffice-config.h>";

# -----------------------------------------------------------------------------

my ($PCRE_MAJOR,$PCRE_MINOR,$PCRE_DATE);
{
    my $filename = "$srcdir/configure.in";
    local (*SRC);
    open (SRC, "<$filename") or
	die "$0: Cannot read $filename: $!\n";
    while (<SRC>) {
	$PCRE_MAJOR = $1 if /^PCRE_MAJOR\s*=\s*(.*)$/;
	$PCRE_MINOR = $1 if /^PCRE_MINOR\s*=\s*(.*)$/;
	$PCRE_DATE = $1 if /^PCRE_DATE\s*=\s*(.*)$/;
    }
    close (*SRC);
}

# Note: pcre_chartables.c is a generated file.
my @SRC_C = ('pcre_exec.c', 'pcre_compile.c', 'pcre_tables.c',
	     'pcre_chartables.c', 'pcre_globals.c', 'pcre_xclass.c',
	     'pcre_info.c', 'pcre_valid_utf8.c', 'pcre_try_flipped.c',
	     'pcre_ord2utf8.c');
my @EXTRA_C = ();
my @SRC_H = ();
my @noinst_H = ('pcre.h', 'pcre_internal.h', 'ucp.h');
my @EXTRA_FILES = ('COPYING');

my %namehack = ('pcre.h' => 'pcre.in');

for my $filename (@SRC_C, @EXTRA_C, @SRC_H, @noinst_H) {
    my $srcfilename = "$srcdir/" . ($namehack{$filename} || $filename);
    my $dstfilename = "$dst/$filename";
    my $tmpfilename = "$dstfilename.new";

    local (*SRC, *DST);
    open (SRC, "<$srcfilename") or
	die "$0: Cannot read $srcfilename: $!\n";
    open (DST, ">$tmpfilename") or
	die "$0: Cannot create $tmpfilename: $!\n";
    print STDERR "Creating $dstfilename...";

    my $where;
    if ($filename =~ /\.h$/) {
	$where = $filename;
	$where =~ s/\.h$/.c/;
    } else {
	$where = 'below';
    }

    print DST "/* File import from pcre to goffice by $myself.  Do not edit.  */\n\n";
    print DST "/* This file has been programatically changed.  */\n";

    my $backchar_fix = 0;
    my $backchar_indent = '';

    if ($filename =~ /printint/) {
	print DST "#include $configfile\n";
    }

    while (<SRC>) {
	s/\@PCRE_MAJOR\@/$PCRE_MAJOR/;
	s/\@PCRE_MINOR\@/$PCRE_MINOR/;
	s/\@PCRE_DATE\@/$PCRE_DATE/;

	s/\b(to(lower|upper)|is(lower|upper|digit|xdigit|space|print|cntrl|alpha|alnum|graph|punct))\b/g_unichar_$1/g;


	if (/^\s*\#\s*include\s*"config\.h"/) {
	    $_ = "#include <goffice/goffice-config.h>\n";
	}

	s/\bEXPORT\b\s*//;

	print DST;
    }

    close (*SRC);
    close (*DST);

    if (1) {
	system ("indent", "-kr", "-fc1", "-psl", "-pcs", $tmpfilename);
	unlink "$tmpfilename~";
    }
    &update_file ($dstfilename);
}

foreach my $filename (@EXTRA_FILES) {
    my $srcfilename = "$srcdir/" . ($namehack{$filename} || $filename);
    my $dstfilename = "$dst/$filename";
    my $tmpfilename = "$dstfilename.new";

    local (*SRC, *DST);
    open (SRC, "<$srcfilename") or
	die "$0: Cannot read $srcfilename: $!\n";
    open (DST, ">$tmpfilename") or
	die "$0: Cannot create $tmpfilename: $!\n";
    print STDERR "Creating $dstfilename...";

    while (<SRC>) {
	print DST;
    }

    close (*SRC);
    close (*DST);
    &update_file ($dstfilename);
}

{
    my $filename = "Makefile.am";
    my $dstfilename = "$dst/$filename";
    my $tmpfilename = "$dstfilename.new";

    local (*DST);
    open (DST, ">$tmpfilename") or
	die "$0: Cannot create $tmpfilename: $!\n";
    print STDERR "Creating $dstfilename...";

    print DST "noinst_LTLIBRARIES = libpcre.la\n";
    print DST "libpcre_la_SOURCES = ", join (' ', @SRC_C), "\n";
    print DST "EXTRA_DIST = ", join (' ', @EXTRA_C, @EXTRA_FILES), "\n\n";

    print DST "libpcre_a_ladir = \$(includedir)/libgoffice-0.3/goffice/cut-n-paste/pcre\n";

    print DST "libpcre_a_la_HEADERS = ", join (' ', @SRC_H), "\n";
    print DST "noinst_HEADERS = ", join (' ', @noinst_H), "\n\n";

    print DST "AM_CPPFLAGS = \\\n";
    print DST "\t\$(GOFFICE_CFLAGS) \\\n";
    print DST "\t-I\$(top_builddir) \\\n";
    print DST "\t-DSUPPORT_UTF8 \\\n";
    print DST "\t-DNEWLINE=10 \\\n";
    print DST "\t-DPOSIX_MALLOC_THRESHOLD=100 \\\n";
    print DST "\t-DLINK_SIZE=2 \\\n";
    print DST "\t-DMATCH_LIMIT=10000000\n\n";

    close (*DST);

    &update_file ($dstfilename);
}

# -----------------------------------------------------------------------------

sub update_file {
    my ($old) = @_;
    my ($new) = "$old.new";

    if (!-r $old) {
	rename $new, $old or
	    die "$0: Cannot rename $new to $old: $!\n";
	print STDERR " -- done.\n";
    } else {
	system ("cmp '$old' '$new' >/dev/null");
	if ($? == 0) {
	    print STDERR " -- unchanged.\n";
	    unlink $new;
	} else {
	    rename $new, $old or
		die "$0: Cannot rename $new to $old: $!\n";
	    print STDERR " -- done.\n";
	}
    }
}

# -----------------------------------------------------------------------------
